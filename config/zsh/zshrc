#!/usr/bin/env zsh

[[ -v $ZDATADIR ]] || source ~/.zshenv

mkdir -p "$ZDATADIR"/{,plugins} "$ZCACHEDIR"

fpath=($ZDATADIR/plugins $fpath)

# Colors
unset LSCOLORS
export CLICOLOR=1

# Explicitly set vi mode for key bindings
bindkey -v

# History Management - keep plenty of history
HISTFILE=$ZDATADIR/zhistory
HISTSIZE=100000
SAVEHIST=100000
setopt EXTENDED_HISTORY          # Write the history file in the ':start:elapsed;command' format.
#setopt SHARE_HISTORY             # Share history between all sessions.
setopt INC_APPEND_HISTORY_TIME   # Append command right after execution.
setopt HIST_EXPIRE_DUPS_FIRST    # Expire a duplicate event first when trimming history.
setopt HIST_IGNORE_DUPS          # Do not record an event that was just recorded again.
setopt HIST_IGNORE_ALL_DUPS      # Delete an old recorded event if a new event is a duplicate.
setopt HIST_FIND_NO_DUPS         # Do not display a previously found event.
setopt HIST_IGNORE_SPACE         # Do not record an event starting with a space.
setopt HIST_SAVE_NO_DUPS         # Do not write a duplicate event to the history file.
setopt HIST_VERIFY               # Do not execute immediately upon history expansion.

# search history with up/down arrows based on what's typed in the prompt
autoload -U history-search-end
zle -N history-beginning-search-backward-end history-search-end
zle -N history-beginning-search-forward-end history-search-end
bindkey "^\e[A" history-beginning-search-backward-end
bindkey "^\e[B" history-beginning-search-forward-end

# Go to folder path without using cd
setopt AUTO_CD
# Turn off all beeps
unsetopt BEEP
# Turn off autocomplete beeps
# unsetopt LIST_BEEP

# Use colors functions to have color at hand
# See https://github.com/zsh-users/zsh/blob/master/Functions/Misc/colors
autoload -Uz colors && colors

# Source my config files
source "$ZDOTDIR/macos.zsh"
source "$ZDOTDIR/completion.zsh"
source "$ZDOTDIR/aliases.zsh"
source "$ZDOTDIR/functions.zsh"
# See https://github.com/jeffreytse/zsh-vi-mode
if [ ! -d "$ZDATADIR/plugins/zsh-vi-mode" ]; then
  git clone https://github.com/jeffreytse/zsh-vi-mode.git "$ZDATADIR/plugins/zsh-vi-mode" &>/dev/null
fi
source "$ZDATADIR/plugins/zsh-vi-mode/zsh-vi-mode.plugin.zsh"
# See https://wezfurlong.org/wezterm/shell-integration.html
if [ ! -f "$ZDATADIR/plugins/wezterm.sh" ]; then
  curl -flo "$ZDATADIR/plugins/wezterm.sh" \
    "https://raw.githubusercontent.com/wez/wezterm/main/assets/shell-integration/wezterm.sh" \
    &>/dev/null
fi
source "$ZDATADIR/plugins/wezterm.sh"


# build the PATH variable
PATH=~/.local/bin:$PATH

# Initialize homebrew
command -v brew &>/dev/null && eval "$(brew shellenv)"

# Add direnv hook
command -v direnv &>/dev/null && eval "$(direnv hook zsh)"

# Initialize miniconda
[ -x "$HOMEBREW_PREFIX/Caskroom/miniconda/base/bin/conda" ] && \
  source "$ZDOTDIR/conda.zsh"

# build prompt
eval "$(starship init zsh)"

